plugins {
   id 'java'
   id "org.xbib.gradle.plugin.git" version "2.0.0"
   id 'project-report'
}

java {
    sourceCompatibility = JavaVersion.toVersion(11)
    targetCompatibility = JavaVersion.toVersion(11)
}

repositories {
    jcenter()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
}

test {
    ignoreFailures = true
    useJUnitPlatform()
}

task reports (dependsOn: ['check', 'javadoc']) {
}

task deployReports(type: Copy, dependsOn: ['reports']){
    from "$buildDir/docs"
    into "$buildDir/tmp/website/docs"

    from "$buildDir/reports"
    into "$buildDir/tmp/website/reports"
}

task clonePages() {                                             
    doLast {
        mkdir 'build/gh-pages'
        def thisRepo = rootProject.projectDir.toString()
        def pagesDir = "$buildDir/gh-pages"
        project.delete {
            delete pagesDir
        }
        def grgit = git.clone {
            dir = pagesDir
            uri = "file:" + thisRepo
            bare = false
            refToCheckout = 'gh-pages'
        }
       grgit.checkout {
           branch = 'gh-pages'
       }
    }
}

task copyReports (type: Copy, dependsOn: ['deployReports', 'clonePages']) {  
    from "$buildDir/tmp/website"
    into "$buildDir/gh-pages"
}

task updateGHPages (dependsOn: 'copyReports') {                     
    group = "Reporting"
    description  'Copies reports to the gh-pages branch in preparation for a future push to GitHub'
    doLast {
        def pagesDir = "$buildDir/gh-pages"
        def grgit = git.open {
            dir = pagesDir + "/.git"
        }
        grgit.add (patterns: ['docs/'])
        grgit.add (patterns: ['reports/'])
        grgit.commit {
            message = "Updating web pages2"
        }
        grgit.push {}
    }
}